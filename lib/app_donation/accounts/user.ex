defmodule AppDonation.Accounts.User do
  use AppDonation, :schema
  import Ecto.Changeset

  schema "users" do
    field :email, :string
    field :first_name, :string
    field :last_name, :string
    field :phone, :string
    field :role, Ecto.Enum, values: [:donor, :organization, :super_admin], default: :donor
    field :password, :string, virtual: true, redact: true
    field :hashed_password, :string, redact: true
    field :confirmed_at, :utc_datetime
    field :admin_approved_at, :utc_datetime
    field :authenticated_at, :utc_datetime, virtual: true
    field :archived, :boolean, default: false
    field :archived_at, :utc_datetime

    has_one :organization, AppDonation.Organizations.Organization
    belongs_to :archived_by_user, AppDonation.Accounts.User, foreign_key: :archived_by

    timestamps(type: :utc_datetime)
  end

  @doc """
  A user changeset for registration with all required fields.

  ## Options

    * `:hash_password` - Hashes the password so it can be stored securely.
      Defaults to `true`.
    * `:validate_unique` - Set to false if you don't want to validate the
      uniqueness of the email. Defaults to `true`.
  """
  def registration_changeset(user, attrs, opts \\ []) do
    user
    |> cast(attrs, [:email, :password, :first_name, :last_name, :phone, :role])
    |> validate_required([:first_name, :last_name, :role])
    |> validate_length(:first_name, min: 1, max: 100)
    |> validate_length(:last_name, min: 1, max: 100)
    |> validate_format(:phone, ~r/^[\d\s\-\+\(\)]*$/, message: "formato de telefono invalido")
    |> validate_inclusion(:role, [:donor, :organization, :super_admin])
    |> validate_email(opts)
    |> validate_confirmation(:password, message: "las contrasenas no coinciden")
    |> validate_password(opts)
  end

  @doc """
  A user changeset for registering or changing the email.

  It requires the email to change otherwise an error is added.

  ## Options

    * `:validate_unique` - Set to false if you don't want to validate the
      uniqueness of the email, useful when displaying live validations.
      Defaults to `true`.
  """
  def email_changeset(user, attrs, opts \\ []) do
    user
    |> cast(attrs, [:email])
    |> validate_email(opts)
  end

  defp validate_email(changeset, opts) do
    changeset =
      changeset
      |> validate_required([:email])
      |> validate_format(:email, ~r/^[^@,;\s]+@[^@,;\s]+$/,
        message: "must have the @ sign and no spaces"
      )
      |> validate_length(:email, max: 160)

    if Keyword.get(opts, :validate_unique, true) do
      changeset
      |> unsafe_validate_unique(:email, AppDonation.Repo)
      |> unique_constraint(:email)
      |> validate_email_changed()
    else
      changeset
    end
  end

  defp validate_email_changed(changeset) do
    if get_field(changeset, :email) && get_change(changeset, :email) == nil do
      add_error(changeset, :email, "did not change")
    else
      changeset
    end
  end

  @doc """
  A user changeset for changing the password.

  It is important to validate the length of the password, as long passwords may
  be very expensive to hash for certain algorithms.

  ## Options

    * `:hash_password` - Hashes the password so it can be stored securely
      in the database and ensures the password field is cleared to prevent
      leaks in the logs. If password hashing is not needed and clearing the
      password field is not desired (like when using this changeset for
      validations on a LiveView form), this option can be set to `false`.
      Defaults to `true`.
  """
  def password_changeset(user, attrs, opts \\ []) do
    user
    |> cast(attrs, [:password])
    |> validate_confirmation(:password, message: "does not match password")
    |> validate_password(opts)
  end

  defp validate_password(changeset, opts) do
    changeset
    |> validate_required([:password])
    |> validate_length(:password, min: 12, max: 72)
    # Examples of additional password validation:
    # |> validate_format(:password, ~r/[a-z]/, message: "at least one lower case character")
    # |> validate_format(:password, ~r/[A-Z]/, message: "at least one upper case character")
    # |> validate_format(:password, ~r/[!?@#$%^&*_0-9]/, message: "at least one digit or punctuation character")
    |> maybe_hash_password(opts)
  end

  defp maybe_hash_password(changeset, opts) do
    hash_password? = Keyword.get(opts, :hash_password, true)
    password = get_change(changeset, :password)

    if hash_password? && password && changeset.valid? do
      changeset
      # If using Bcrypt, then further validate it is at most 72 bytes long
      |> validate_length(:password, max: 72, count: :bytes)
      # Hashing could be done with `Ecto.Changeset.prepare_changes/2`, but that
      # would keep the database transaction open longer and hurt performance.
      |> put_change(:hashed_password, Bcrypt.hash_pwd_salt(password))
      |> delete_change(:password)
    else
      changeset
    end
  end

  @doc """
  Confirms the account by setting `confirmed_at`.
  """
  def confirm_changeset(user) do
    now = DateTime.utc_now(:second)
    change(user, confirmed_at: now)
  end

  @doc """
  Approves the user by admin, setting `admin_approved_at`.
  """
  def admin_approve_changeset(user) do
    now = DateTime.utc_now(:second)
    change(user, admin_approved_at: now)
  end

  @doc """
  Archives the user, setting `archived`, `archived_at` and `archived_by`.
  """
  def archive_changeset(user, archived_by_id) do
    now = DateTime.utc_now(:second)
    change(user, archived: true, archived_at: now, archived_by: archived_by_id)
  end

  @doc """
  Unarchives the user, clearing `archived`, `archived_at` and `archived_by`.
  """
  def unarchive_changeset(user) do
    change(user, archived: false, archived_at: nil, archived_by: nil)
  end

  @doc """
  Checks if a requester user needs admin approval.
  """
  def needs_admin_approval?(%__MODULE__{role: :organization, admin_approved_at: nil}), do: true
  def needs_admin_approval?(_), do: false

  @doc """
  Verifies the password.

  If there is no user or the user doesn't have a password, we call
  `Bcrypt.no_user_verify/0` to avoid timing attacks.
  """
  def valid_password?(%AppDonation.Accounts.User{hashed_password: hashed_password}, password)
      when is_binary(hashed_password) and byte_size(password) > 0 do
    Bcrypt.verify_pass(password, hashed_password)
  end

  def valid_password?(_, _) do
    Bcrypt.no_user_verify()
    false
  end
end
